options {
	JAVA_UNICODE_ESCAPE = true;
	STATIC = false;
}
PARSER_BEGIN(SQLParser)

	package parser;
	
	import java.util.*;
	import ast.*;
	import schema.*;

	public class SQLParser {

	}
PARSER_END(SQLParser)

// Lexical definitions --------------------------------------------------------

// whitespace
SKIP :
{
    <" "> | <"\t"> | <"\n"> | <"\f"> | <"\r">
}
// Comments
SKIP :
{
    < "--" (~["\n"])* "\n" >
|	< "{" (~["}"])* "}" >
//|   < "/*" ( ~["*"] | ("*")+ ~["*","/"] )* ("*")+ "/" >
}

// SQL keywords
TOKEN [IGNORE_CASE]:
{
	< KW_AND: "AND" >
|	< KW_OR: "OR" >
|	< KW_CREATE: "CREATE" >
|	< KW_CHECK: "CHECK" >
|	< KW_PRIMARY: "PRIMARY" >
|	< KW_FOREIGN: "FOREIGN" >
|	< KW_KEY: "KEY" >
|	< KW_REFERENCES: "REFERENCES" >
|	< KW_DROP: "DROP" >
|	< KW_TABLE: "TABLE" >
|	< KW_SELECT: "SELECT" >
|	< KW_FROM: "FROM" >
|	< KW_WHERE: "WHERE" >
|	< KW_INSERT: "INSERT" >
|	< KW_INTO: "INTO" >
|	< KW_VALUES: "VALUES" >
|	< KW_DELETE: "DELETE" >
|	< KW_UPDATE: "UPDATE" >
|	< KW_SET: "SET" >
|	< KW_HELP: "HELP" >
|	< KW_TABLES: "TABLES" >
|	< KW_DESCRIBE: "DESCRIBE" >
|	< KW_QUIT: "QUIT" >

|	< KW_INT: "int" >
|	< KW_CHAR: "char" >
|	< KW_DECIMAL: "decimal" >
}

// SQL symbols
TOKEN:
{
	< SYM_PLUS: "+" >
|	< SYM_MINUS: "-" >
|	< SYM_STAR: "*" >
|	< SYM_SLASH: "/" >
|	< SYM_EQUAL: "=" >
|	< SYM_EXCLEQUAL: "!=" >
|	< SYM_LESS: "<" >
|	< SYM_MORE: ">" >
|	< SYM_LESSEQUAL: "<=" >
|	< SYM_MOREEQUAL: ">=" >
}

// SQL identifiers
TOKEN:
{
	< IDENTIFIER: ["a"-"z","A"-"Z"](["_","0"-"9","a"-"z","A"-"Z"])* >
}

// literals
TOKEN:
{
	< INT_LITERAL: (["0"-"9"])+ >
|	< STRING_LITERAL: "'"(~["\\","'"]|"''"|"\\'")*"'" >	// ??? might be wrong
}


// Grammar --------------------------------------------------------------------

Command Command():
{
	Command ret;
	//System.out.println("Command()");
}
{	ret = CreateTable()
|	ret = DropTable()
|	ret = Select()
|	ret = Insert()
|	ret = Delete()
|	ret = Update()
|	LOOKAHEAD(2) ret = HelpTables()
|	LOOKAHEAD(2) ret = HelpDescribe()
|	ret = HelpCommand()
|	ret = Quit()
	
	{ return ret; }
}

// CREATE TABLE ( ... ) -------------------------------------------------------
CreateTableCommand CreateTable():
{
	String table;
	List<Attribute> attributes = new ArrayList<Attribute>();
	List<String> primaryKey = new ArrayList<String>();
	Map<String, Attribute> foreignKeys = new HashMap<String, Attribute>();
	
	Attribute attribute;

	// for foreign key stuff
	String name;
	String foreignTable;
	String foreignName;

	//System.out.println("CreateTable()");
}
{
	< KW_CREATE > < KW_TABLE > < IDENTIFIER >
	{ table = token.image; }
	"("
	(
		(
			( ( attribute = AttrDecl() ) { attributes.add(attribute.setTable(table)); } ",")+
		)
		(			< KW_PRIMARY > < KW_KEY >
			"("
				( < IDENTIFIER > { primaryKey.add(token.image); } )				( "," < IDENTIFIER > { primaryKey.add(token.image); } )*
			")"
		)
		(
			"," < KW_FOREIGN > < KW_KEY > "(" ( < IDENTIFIER > { name = token.image; } ) ")"
			< KW_REFERENCES > ( < IDENTIFIER > { foreignTable = token.image; } )
			"(" ( < IDENTIFIER > { foreignName = token.image; } ) ")"
			{ foreignKeys.put(name, new Attribute(foreignTable, foreignName)); }
		)*	)
	");"	{ return new CreateTableCommand(token, table, attributes, primaryKey, foreignKeys); }
}

Attribute AttrDecl():
{
	String name;
	Attribute.Type type;
	int length = -1;
	Exp constraint = null;

	//System.out.println("AttrDecl()");}{
	( < IDENTIFIER > { name = token.image; } )
	(			
		(			< KW_INT > { type = Attribute.Type.INT; }
		)
	|	(
			< KW_CHAR > "(" < INT_LITERAL > { length=Integer.parseInt(token.image); } ")"			{ type = Attribute.Type.CHAR; } 		)
	|	(
			< KW_DECIMAL > { type = Attribute.Type.DECIMAL; }
		)
	)
	(
		// the constraint exp should have an expString generated
		< KW_CHECK >
		"(" (
				constraint = Expression()
		) ")"	)?
	{ return new Attribute(null, type, length, name, constraint); }}


// DROP TABLE table -----------------------------------------------------------
DropTableCommand DropTable():
{
	//System.out.println("DropTable()");
}
{
	< KW_DROP > < KW_TABLE > < IDENTIFIER > ";"
	{ return new DropTableCommand(token, token.image); }
}


// SELECT attr0,... FROM table0,... WHERE conditions --------------------------
SelectCommand Select():
{
	List<AttributeExp> attributes = new ArrayList<AttributeExp>();
	List<String> tables = new ArrayList<String>();
	Exp conditions;

	//System.out.println("Select()");
}
{
	< KW_SELECT >
	(	
		(			( < IDENTIFIER > { attributes.add(new AttributeExp(token, token.image)); } )
			( "," < IDENTIFIER > { attributes.add(new AttributeExp(token, token.image)); } )*
		)
	|	< SYM_STAR >
	)
	< KW_FROM >
	( < IDENTIFIER > { tables.add(token.image); } )
	( "," < IDENTIFIER > { tables.add(token.image); } )*
	< KW_WHERE > ( conditions = Expression() ) ";"
	{ return new SelectCommand(token, attributes, tables, conditions); }
}


// INSERT INTO table VALUES(val1,...) -----------------------------------------
InsertCommand Insert():
{
	String table;
	List<Exp> values = new ArrayList<Exp>();

	Exp value;
	
	//System.out.println("Insert()");
}
{
	< KW_INSERT > < KW_INTO > < IDENTIFIER > { table = token.image; }
	< KW_VALUES > "("
	( ( value = Expression() ) { values.add(value); } )
	( "," ( value = Expression() ) { values.add(value); } )*
	");"
	{ return new InsertCommand(token, table, values); }
}


// DELETE FROM table WHERE conditions -----------------------------------------
DeleteCommand Delete():
{
	String table;
	Exp conditions;
	//System.out.println("Delete()");
}
{
	< KW_DELETE > < KW_FROM >	< IDENTIFIER > { table = token.image; }
	< KW_WHERE > ( conditions = Expression() ) ";"
	{ return new DeleteCommand(token, table, conditions); }
}


// UPDATE table SET attr1=val1, ... WHERE conditions --------------------------
UpdateCommand Update():
{
	String table;
	List<AttributeAssign > assignments = new ArrayList<AttributeAssign>();
	Exp conditions;

	AttributeAssign assignment;

	//System.out.println("Update()");
}
{
	< KW_UPDATE > < IDENTIFIER > { table = token.image; }	< KW_SET > ( ( assignment = AttributeAssignr() ) { assignments.add(assignment); } )	( "," ( assignment = AttributeAssignr() ) { assignments.add(assignment); } )*
	< KW_WHERE > ( conditions = Expression() ) ";"
	{ return new UpdateCommand(token, table, assignments, conditions); }
}

AttributeAssign AttributeAssignr():
{
	AttributeExp target;
	Exp value;
	//System.out.println("AttributeAssignr()");}
{
	< IDENTIFIER > { target = new AttributeExp(token, token.image); }	< SYM_EQUAL > ( value = Expression() )
	{ return new AttributeAssign(token, target, value); }}


// HELP TABLES ----------------------------------------------------------------
HelpTablesCommand HelpTables():
{
	//System.out.println("HelpTables()");
}
{
	< KW_HELP > < KW_TABLES > ";"	{ return new HelpTablesCommand(token); }
}

// HELP DESCRIBE table --------------------------------------------------------
HelpDescribeCommand HelpDescribe():
{
	//System.out.println("HelpDescribe()");
}
{
	< KW_HELP > < KW_DESCRIBE > < IDENTIFIER > ";"
	{ return new HelpDescribeCommand(token, token.image); }
}


// HELP command ---------------------------------------------------------------
HelpCommandCommand HelpCommand():
{
	HelpCommandCommand.Type type;
	//System.out.println("HelpCommand()");
}
{
	< KW_HELP >
	(
		(			< KW_CREATE > < KW_TABLE >
			{ type = HelpCommandCommand.Type.CREATE_TABLE; }
		)
	|	(
			< KW_DROP > < KW_TABLE >
			{ type = HelpCommandCommand.Type.DROP_TABLE; }
		)
	|	(
			< KW_SELECT >
			{ type = HelpCommandCommand.Type.SELECT; }
		)
	|	(
			< KW_INSERT >
			{ type = HelpCommandCommand.Type.INSERT; }
		)
	|	(
			< KW_DELETE >
			{ type = HelpCommandCommand.Type.DELETE; }
		)
	|	(
			< KW_UPDATE >
			{ type = HelpCommandCommand.Type.UPDATE; }
		)
	) ";"
	{ return new HelpCommandCommand(token, type); }
}


// QUIT -----------------------------------------------------------------------
QuitCommand Quit():
{
	//System.out.println("Quit()");
}
{
	< KW_QUIT > ";"	{ return new QuitCommand(token); }
}





// Expression *****************************************************************

Exp Expression():
{
	Exp ret;
	//System.out.println("Expression()");
}
{
	{ Exp.clearGlobalExpString(); }
	ret = LogicOp()
	{ ret.saveExpString();  return ret; }
}


// CmpOp AND CmpOp OR CmpOp AND CmpOp ... left-binding
Exp LogicOp():
{
	Exp ret;
	BinaryExp postfix;
	//System.out.println("LogicOp()");
}
{
	( ret = CmpOp() )
	(
		( postfix = LogicOp_postfix() )
		{ postfix.setLeft(ret);  ret = postfix; }
	)*
	{ return ret; }
}
BinaryExp LogicOp_postfix():
{
	Token op;
	Exp right;
	//System.out.println("LogicOp_postfix()");
}
{
	( < KW_AND > | < KW_OR > )
	{ op = token;  Exp.appendToGlobalExpString(token.image.toUpperCase()+" "); }
	( right = CmpOp() )
	{ return new BinaryExp(null, op, right); }
}


// AddOp < AddOp = AddOp != AddOp >= AddOp ... left-binding
Exp CmpOp():
{
	Exp ret;
	BinaryExp postfix;
	//System.out.println("CmpOp()");
}
{
	( ret = AddOp() )
	(
		( postfix = CmpOp_postfix() )
		{ postfix.setLeft(ret);  ret = postfix; }
	)*
	{ return ret; }
}
BinaryExp CmpOp_postfix():
{
	Token op;
	Exp right;
	//System.out.println("CmpOp_postfix()");
}
{
	( < SYM_LESS > | < SYM_LESSEQUAL > | < SYM_EQUAL >
	| < SYM_EXCLEQUAL > | < SYM_MORE > | < SYM_MOREEQUAL > )
	{ op =  token;  Exp.appendToGlobalExpString(token.image); }
	( right = AddOp() )
	{ return new BinaryExp(null, op, right); }
}


// MulOp + MulOp - MulOp ... left-binding 
Exp AddOp():
{
	Exp ret;
	BinaryExp postfix;
	//System.out.println("AddOp()");
}
{
	( ret = MulOp() )
	(
		( postfix = AddOp_postfix() )
		{ postfix.setLeft(ret);  ret = postfix; }
	)*
	{ return ret; }
}
BinaryExp AddOp_postfix():
{
	Token op;
	Exp right;
	//System.out.println("AddOp_postfix()");
}
{
	( < SYM_PLUS > | < SYM_MINUS > )
	{ op = token;  Exp.appendToGlobalExpString(token.image); }
	( right = MulOp() )
	{ return new BinaryExp(null, op, right); }
}


// UnaryOp * UnaryOp / UnaryOp * UnaryOp ... left-binding
Exp MulOp():
{
	Exp ret;
	BinaryExp postfix;
	//System.out.println("MulOp()");
}
{
	( ret = UnaryOp() )
	(
		( postfix = MulOp_postfix() )
		{ postfix.setLeft(ret);  ret = postfix; }
	)*
	{ return ret; }
}
BinaryExp MulOp_postfix():
{
	Token op;
	Exp right;
	//System.out.println("MulOp_postfix()");
}
{
	( < SYM_STAR > | < SYM_SLASH > )
	{ op = token;  Exp.appendToGlobalExpString(token.image); }
	( right = UnaryOp() )
	{ return new BinaryExp(null, op, right); }
}


// +PrimaryExp, -PrimaryExp
Exp UnaryOp():
{
	Exp ret;
	Exp sub;
	String op;
	//System.out.println("UnaryOp()");
}
{
	(		(			
			( < SYM_PLUS > | < SYM_MINUS > )
			{ op = token.image;  Exp.appendToGlobalExpString(token.image); }					( sub = PrimaryExp() )
			{ ret = new UnaryExp(token, op, sub); }
		)
	|	(
			( ret = PrimaryExp() )
		)
	)
	{ return ret; }
}


Exp PrimaryExp():
{
	Exp ret;
	//System.out.println("PrimaryExp()");
}
{
	(
		(
			< INT_LITERAL >
			{ ret = new IntLiteralExp(token, Integer.parseInt(token.image));  Exp.appendToGlobalExpString(token.image); }
		)
	|	(
			< STRING_LITERAL >
			{ ret = new StringLiteralExp(token, token.image);  Exp.appendToGlobalExpString(token.image); }
		)
	|	(
			< IDENTIFIER >
			{ ret = new AttributeExp(token, token.image);  Exp.appendToGlobalExpString(token.image); }
		)
	|	(
			"(" { Exp.appendToGlobalExpString('('); }			( ret = LogicOp() )
			")" { Exp.appendToGlobalExpString(')'); }
		)
	)
	{ return ret; }
}
